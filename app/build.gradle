apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {

    compileSdkVersion 29

    defaultConfig {
        applicationId "com.espressif.rainmaker"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 12
        versionName "2.2.1 - ${getGitHash()}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "GitHash", "\"${getGitHash()}\""
            resValue "string", "security_type", "sec1"
            resValue "string", "proof_of_possesion", "abcd1234"
            resValue "string", "aws_region", ""
            resValue "string", "user_pool_id", "your_user_pool_id"
            resValue "string", "client_id", "your_client_id"
            buildConfigField "boolean", "isScheduleSupported", "true"
            buildConfigField "boolean", "isLocalControlSupported", "true"
        }

        debug {
            debuggable true
            buildConfigField "String", "GitHash", "\"${getGitHash()}\""
            resValue "string", "security_type", "sec1"
            resValue "string", "proof_of_possesion", "abcd1234"
            resValue "string", "aws_region", ""
            resValue "string", "user_pool_id", "your_user_pool_id"
            resValue "string", "client_id", "your_client_id"
            buildConfigField "boolean", "isScheduleSupported", "true"
            buildConfigField "boolean", "isLocalControlSupported", "true"
        }
    }

    buildToolsVersion '28.0.3'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    implementation 'com.google.crypto.tink:tink-android:1.1.0'

    implementation 'com.wang.avi:library:2.1.3'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.auth0.android:jwtdecode:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation 'com.aventrix.jnanoid:jnanoid:2.0.0'
    implementation 'com.budiyev.android:code-scanner:2.1.0'
    implementation 'com.github.espressif:esp-idf-provisioning-android:lib-2.0.6'

    implementation 'com.amazonaws:aws-android-sdk-cognitoidentityprovider:2.19.0'
    implementation 'com.github.warkiz.tickseekbar:tickseekbar:0.1.4'
    implementation('com.amazonaws:aws-android-sdk-iot:2.15.2') { transitive = true }
    implementation('com.amazonaws:aws-android-sdk-mobile-client:2.15.2') { transitive = true }

    // Room dependencies
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
